# G√©n√©rateur de sous-titres avec Whisper et FFmpeg (Windows)

Ce d√©p√¥t contient un guide complet et un script .bat pour g√©n√©rer automatiquement un fichier de sous-titres √† partir de vid√©os .mp4, gr√¢ce √† FFmpeg et OpenAI Whisper.

Whisper peut transcrire l'audio de votre vid√©o dans sa langue d'origine. De plus, il offre la possibilit√© (optionnelle) de traduire cette transcription en anglais.
---> [https://github.com/openai/whisper](Whisper)

‚ö†Ô∏è Si vous choisissez l'option de traduction, notez que la traduction du texte obtenu vers le fran√ßais ou toute autre langue autre que l'anglais devra √™tre effectu√©e s√©par√©ment, car ***Whisper ne prend en charge la traduction que vers l'anglais.***

---

## üìã Pr√©requis

- Windows 10 ou sup√©rieur
- Python 3.9+ (avec pip)
- FFmpeg (build essentials)
- Connexion internet (pour l‚Äôinstallation des d√©pendances Whisper)
- Whisper install√© via `pip`
- pip install rich pyfiglet (python script only)

---


## 1. Installation

### 1.1 Installer Python

- T√©l√©charger Python : [https://www.python.org/downloads/](https://www.python.org/downloads/)
- Lors de l'installation, **cochez "Add Python to PATH"**

### 1.2 Installer FFmpeg

- T√©l√©charger FFmpeg (build essentials) : [https://www.gyan.dev/ffmpeg/builds/ffmpeg-release-essentials.zip](https://www.gyan.dev/ffmpeg/builds/ffmpeg-release-essentials.zip)
- D√©compressez dans un dossier simple (ex : `C:\Outils\ffmpeg\`)
- V√©rifiez l‚Äôex√©cutable avec `ffmpeg.exe -version` dans un terminal ouvert dans ce dossier

### 1.3 Installer Whisper et d√©pendances Python

Ouvrez un terminal (cmd ou PowerShell) et lancez :

```bash
python -m pip install --upgrade pip
pip install git+https://github.com/openai/whisper.git
pip install ffmpeg-python
```

## 2. Utilisation

### 2.1 Commandes s√©par√©es

```bash
# 1. Extraire l‚Äôaudio en WAV optimis√© pour Whisper
ffmpeg -i mavideo.mp4 -ar 16000 -ac 1 -c:a pcm_s16le audio.wav

# 2. Transcrire l'audio en sous-titres (dans la langue de l'audio)
whisper audio.wav --model small --language en

# 3. Transcrire + traduire en anglais (video en francais par exemple, sous-titres en anglais)
whisper audio.wav --model small --language fr --task translate

```

### 2.2 Script `.bat` automatique (glisser-d√©poser)

Placez ce fichier `gen_subs.bat` dans le m√™me dossier que `ffmpeg.exe` et vos vid√©os `.mp4`.  
Glissez-d√©posez une vid√©o dessus : le script va extraire l‚Äôaudio, lancer Whisper pour transcrire et traduire, puis supprimer l‚Äôaudio temporaire.

```bat
@echo off
setlocal enabledelayedexpansion

if "%~1"=="" (
  echo [!] Glisse une video dessus pour generer les sous-titres.
  pause
  exit /b
)

set "video=%~1"
set "basename=%~n1"

REM --- v√©rif outils ---
if not exist ffmpeg.exe (
  where ffmpeg >nul 2>&1 || (
    echo [!] ffmpeg introuvable. Mets ffmpeg.exe dans ce dossier ou ajoute-le au PATH.
    pause
    exit /b
  )
)
where whisper >nul 2>&1
if errorlevel 1 (
  echo [!] whisper introuvable. Assure-toi que Whisper est installe et que la commande "whisper" est sur le PATH.
  pause
  exit /b
)

echo -------------------------------------------------------
echo Video cible : "%video%"
echo Nom base   : "%basename%"
echo -------------------------------------------------------

REM --- langue source ---
set "lang_arg="
set /p inlang=Langue source de l'audio (ex: en, fr, es) [auto] : 
if not "%inlang%"=="" (
  set "lang_arg=--language %inlang%"
  echo [*] Langue forcee : %inlang%
) else (
  echo [*] Auto-detection de la langue activ√©e.
)

REM --- choix transcription vs traduction vers l'anglais ---
echo.
echo Veux-tu traduire vers l'anglais (quelle que soit la langue source) ?
choice /M "Traduire vers l'anglais ?"
if errorlevel 2 (
  set "task_arg="
  echo [*] Mode : transcription seule.
) else (
  set "task_arg=--task translate"
  echo [*] Mode : traduction vers l'anglais.
)

echo.
echo [1/3] Extraction audio...
ffmpeg -hide_banner -loglevel error -i "%video%" -ar 16000 -ac 1 -c:a pcm_s16le "%basename%.wav"
if errorlevel 1 (
  echo [!] echec de l'extraction audio.
  pause
  exit /b
)

echo.
echo [2/3] Lancement de Whisper...
whisper "%basename%.wav" --model tiny %lang_arg% %task_arg%
if errorlevel 1 (
  echo [!] Whisper a echoue. Le .srt peut etre incomplet ou absent.
) else (
  echo [*] Whisper a termine.
)

echo.
echo [3/3] Nettoyage...
if exist "%basename%.wav" del "%basename%.wav" >nul 2>&1

echo.
echo -------------------------------------------------------
echo Sous-titres generes : "%basename%.srt"
echo -------------------------------------------------------
pause

```
### 2.3 R√©sultat
- Plusieurs types de fichiers sont g√©n√©r√©s dans le m√™me dossier que la vid√©o, incluant :

  `.srt` (SubRip) : Le format de sous-titres le plus courant, compatible avec la plupart des lecteurs vid√©o (comme VLC).

  `.json` (JavaScript Object Notation) : Un format structur√© pour les donn√©es, utile pour l'int√©gration dans des applications.

  `.txt` (texte brut) : Une simple transcription du texte.

  `.tsv` (Tab Separated Values) : Un format de tableau simple, s√©par√© par des tabulations.

  `.vtt` (WebVTT) : Un autre format de sous-titres, souvent utilis√© pour le web.

- Pour la lecture vid√©o :

  - Si le fichier .srt porte exactement le m√™me nom que la vid√©o (par exemple, ma_video.mp4 et ma_video.srt), VLC Media Player le chargera automatiquement.

  - Sinon, vous pouvez charger manuellement le fichier .srt depuis VLC via : Menu ‚Üí Sous-titres ‚Üí Ajouter un fichier de sous-titres‚Ä¶

---

## 3. Notes

- **Vitesse vs pr√©cision** :
  - `--model tiny` ‚Üí tr√®s rapide, moins pr√©cis
  - `--model small` ‚Üí rapide et pr√©cis pour la plupart des cas
  - `--model medium` ou `large` ‚Üí tr√®s pr√©cis, mais plus lent
- **Langue source** :
  - Ici `--language en` indique que l‚Äôaudio est en anglais.
  - Changez-le si la vid√©o est dans une autre langue (ex: `--language fr`).
- **Traduction** :
  - `--task translate` traduit automatiquement **vers l'anglais**.
  - Pour traduire vers une autre langue, transcrivez d‚Äôabord (`--task transcribe`), puis utilisez un traducteur externe.
- Le script supprime le `.wav` temporaire apr√®s g√©n√©ration du `.srt` pour garder le dossier propre.

---

## 4. Exemple d‚Äôusage rapide

1. Glissez `ma_video.mp4` sur `gen_subs.bat`.
2. Attendez que le script ait termin√© l‚Äôextraction audio, la transcription et la traduction.
3. Ouvrez `ma_video.mp4` dans VLC.  
   - Si le `.srt` a le m√™me nom que la vid√©o, il se chargera automatiquement.
   - Sinon, ajoutez-le via le menu des sous-titres.
  
---

Aper√ßus du script batch:

| Script en cours d‚Äôex√©cution | 
|-----------------------------|
| ![Execution Script](docs/screenshot_script.PNG) |


---



## 5. Script Python interactif (gen_subs.py)

- En plus du script .bat, ce d√©p√¥t inclut une version Python plus √©volu√©e, interactive et visuellement enrichie gr√¢ce √† Rich et PyFiglet.

Cette version :

 - Affiche une banni√®re ASCII r√©tro et des tableaux color√©s.

 - V√©rifie automatiquement la pr√©sence de ffmpeg et whisper dans le PATH.

 - Propose un mode interactif pour choisir :

    - Le mod√®le Whisper (tiny, base, small, medium, large)

    - La langue source (ou autod√©tection)

    - La traduction vers l‚Äôanglais (oui/non)

- Fournit un r√©capitulatif complet et la langue d√©tect√©e.

### Utilisation

```python gen_subs.py ma_video.mp4```

Le script vous posera ensuite les questions n√©cessaires.

Vous pouvez aussi tout pr√©ciser en ligne de commande :

```python gen_subs.py ma_video.mp4 --model small --language fr --translate-to-en```

Options disponibles :

`--model`            Taille du mod√®le Whisper (tiny, base, small, medium, large)
`--language, -l `    Code ISO de la langue source (ex: fr, en) ‚Äì vide = autod√©tection
`--translate-to-en`  Traduire automatiquement vers l'anglais
`--no-clean`         Conserver le fichier .wav temporaire
`--log `             Fichier o√π enregistrer le rapport d‚Äôex√©cution

---

Aper√ßus du script python :

| Script en cours d‚Äôex√©cution | 
|-----------------------------|
| ![Execution Script](docs/screenshot_scriptpython.PNG) |


---


